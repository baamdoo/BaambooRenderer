#version 460
#extension GL_GOOGLE_include_directive : require

#include "NoiseCommon.hg"
#include "HelperFunctions.hg"

layout(set = 1, binding = 0, rgba8) writeonly uniform image2D g_OutWeatherMap;

layout (local_size_x = 8, local_size_y = 8) in;
void main()
{
    ivec2 imgSize   = imageSize(g_OutWeatherMap);
    ivec2 pixCoords = ivec2(gl_GlobalInvocationID.xyz);
    if (any(greaterThanEqual(pixCoords, imgSize)))
        return;

    vec2  uv        = vec2(pixCoords + 0.5) / vec2(imgSize);

    const uint seed = 0x1u;
    float perlin = perlinFBM(uv, 4.0, 6, 0.5, 2.0, seed);
          perlin = (perlin + 1.0) * 0.5;
          perlin = remap(perlin, 0.2, 1.0, 0.0, 1.0);
    float worley = worleyFBM(uv, 8.0);

    float coverage  = remap(perlin, 1.0 - worley, 1.0, 0.0, 1.0);
    float cloudType = perlinFBM(uv, 16.0, 10, 0.3, 2.0);

    vec4 weather = vec4(perlin, saturate(coverage), saturate(cloudType), 1.0);

    imageStore(g_OutWeatherMap, pixCoords, weather);
}