#version 460
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_GOOGLE_include_directive : require

#define _CAMERA
#define _SCENEENVIRONMENT
#include "AtmosphereCommon.hg"

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(set = 1, binding = 2) uniform sampler2D g_TransmittanceLUT;
layout(set = 1, binding = 3) uniform sampler2D g_MultiScatteringLUT;
layout(set = 1, binding = 4, r11f_g11f_b10f) uniform writeonly image2D g_OutSkyViewLUT;

layout(push_constant) uniform PushConstants
{
    uint minRaySteps;
    uint maxRaySteps;
} g_Push;

vec3 GetSkyViewRayDirectionFromUV(vec2 uv, float viewHeight)
{
    AtmosphereData Atmosphere = GetAtmosphereData();

	float Vhorizon           = sqrt(viewHeight * viewHeight - Atmosphere.planetRadiusKm * Atmosphere.planetRadiusKm);
	float cosBeta            = Vhorizon / viewHeight;				
	float beta               = acosFast4(cosBeta);
	float zenithHorizonAngle = PI - beta;

	float latitude;
	if (uv.y < 0.5)
	{
		float coord = 1.0 - 2.0 * uv.y;
		coord      *= coord;
		coord       = 1.0 - coord;

		latitude = zenithHorizonAngle * coord;
	}
	else
	{
		float coord = uv.y * 2.0 - 1.0;
		coord      *= coord;

		latitude = zenithHorizonAngle + beta * coord;
	}

	float longitude = uv.x * 2.0 * PI + PI; // '+PI' to resolve 180 degrees mis-unligned between texture coordinate and spheric coordinate

	float cosLatitude  = cos(latitude);
    float sinLatitude  = sin(latitude);
	float cosLongitude = cos(longitude);
    float sinLongitude = sin(longitude);
	vec3 viewDir = vec3(
		    sinLatitude * cosLongitude,
		    cosLatitude,
		    sinLatitude * sinLongitude
		);

    return viewDir;
}

vec4 RayMarchScattering(vec3 rayOrigin, vec3 rayDir, float maxDistance) 
{
    AtmosphereData Atmosphere = GetAtmosphereData();

    vec2 atmosphereIntersection = RaySphereIntersection(rayOrigin, rayDir, PLANET_CENTER, Atmosphere.atmosphereRadiusKm);
    if (atmosphereIntersection.y < 0.0)
        return vec4(0.0);
    
    float rayStart  = max(0.0, atmosphereIntersection.x);
    float rayLength = min(maxDistance, atmosphereIntersection.y) - rayStart;
    if (rayLength <= 0.0)
        return vec4(0.0);

    // light illuminance
    vec3 lightColor = vec3(Atmosphere.light.colorR, Atmosphere.light.colorG, Atmosphere.light.colorB);
    if (Atmosphere.light.temperatureK > 0.0)
        lightColor *= ColorTemperatureToRGB(Atmosphere.light.temperatureK);

    vec3 E = Atmosphere.light.illuminanceLux * lightColor;

    // phase functions
    vec3 sunDirection  = vec3(-Atmosphere.light.dirX, -Atmosphere.light.dirY, -Atmosphere.light.dirZ);

    float cosTheta      = dot(rayDir, sunDirection);
    float phaseRayleigh = RayleighPhase(cosTheta);
    float phaseMie      = MiePhase(cosTheta, Atmosphere.miePhaseG);

    // variable sampling count according to rayLength
    float numSteps = float(g_Push.maxRaySteps);//mix(float(g_Push.minRaySteps), float(g_Push.maxRaySteps), clamp(rayLength / 150.0, 0.0, 1.0));
    float stepSize = rayLength / numSteps;

    vec3 L          = vec3(0.0);
    vec3 throughput = vec3(1.0);
    for (float i = 0.0; i < numSteps; i += 1.0) 
    {
        float t   = rayStart + (float(i) + 0.5) * stepSize;
        vec3  pos = rayOrigin + t * rayDir;
        
        // skip if below ground
        float sampleHeight = length(pos);
        if (sampleHeight < Atmosphere.planetRadiusKm) 
            break;

        // skip if above atmosphere
        if (sampleHeight > Atmosphere.atmosphereRadiusKm)
            continue;

        float sampleAltitude = sampleHeight - Atmosphere.planetRadiusKm;
        
        // extinction(out-scattering) at sample point
        float rayleighDensity = GetDensityAtHeight(sampleAltitude, Atmosphere.rayleighDensityKm);
        float mieDensity      = GetDensityAtHeight(sampleAltitude, Atmosphere.mieDensityKm);
        float ozoneDensity    = GetDensityOzoneAtHeight(sampleAltitude, Atmosphere.ozoneCenterKm, Atmosphere.ozoneWidthKm);

        vec3  rayleighScattering = Atmosphere.rayleighScattering * rayleighDensity;
        float mieScattering      = Atmosphere.mieScattering * mieDensity;
        float mieAbsorption      = Atmosphere.mieAbsorption * mieDensity;
        vec3  ozoneAbsorption    = Atmosphere.ozoneAbsorption * ozoneDensity;
        
        vec3 scattering        = rayleighScattering + vec3(mieScattering);
        vec3 extinction        = rayleighScattering + vec3(mieScattering + mieAbsorption) + ozoneAbsorption;
        vec3 phasedScattering  = phaseRayleigh * rayleighScattering + phaseMie * mieScattering; // σs(x) * p(v,l)
        vec3 stepTransmittance = exp(-extinction * stepSize);                                   // (2) T(xa,xb) = exp(−Integral(xa~xb, σ(x)dx))
        
        // transmittance from sample point to sun
        float sampleTheta        = dot(normalize(pos), sunDirection);
        vec3  transmittanceToSun = SampleTransmittanceLUT(g_TransmittanceLUT, sampleHeight, sampleTheta, Atmosphere.planetRadiusKm, Atmosphere.atmosphereRadiusKm);

        // multi-scattering
        vec2 msUV = clamp(
                        vec2(sampleTheta * 0.5 + 0.5, inverseLerp(sampleHeight, Atmosphere.planetRadiusKm, Atmosphere.atmosphereRadiusKm)),
                    0.0, 1.0);
        vec3 multiScattering = texture(g_MultiScatteringLUT, msUV).rgb;
        
        // planet shadow
        vec2  planetIntersection = RaySphereIntersection(pos, sunDirection, PLANET_CENTER, Atmosphere.planetRadiusKm);
        float planetShadow       = planetIntersection.x < 0.0 ? 1.0 : 0.0;

        {
            // (4) S(x,li) = Vis(l_i) * T(x,x+t*li)
            // vec3 S = planetShadow * transmittanceToSun;
            // (3) Lscat(c,x,v) = σs(x) * (T(c,x) * S(x,l) * p(v,l) + Ψms) * E * dt
            // L     += (throughput * S * phasedScattering + multiScattering * scattering) * E * stepSize;

            // Analytical integration as proposed in slide 28 of http://www.frostbite.com/2015/08/physically-based-unified-volumetric-rendering-in-frostbite/
            vec3 S    = (planetShadow * transmittanceToSun * phasedScattering + multiScattering * scattering) * E;
            vec3 Sint = (S - S * stepTransmittance) / max(extinction, 1e-8);
            L        += throughput * Sint;
        }
        throughput *= stepTransmittance;
    }
    
    // xyz: (3) Lscat(c,x,v), w: (1) T(c,p) (Lo will be evaluated later stage)
    return vec4(L, dot(throughput, vec3(1.0 / 3.0)));
}

void main() 
{
    ivec2 imgSize   = imageSize(g_OutSkyViewLUT);
    ivec2 pixCoords = ivec2(gl_GlobalInvocationID.xy);
    if (any(greaterThanEqual(pixCoords, imgSize)))
        return;

    vec2  uv        = (vec2(pixCoords) + 0.5) / vec2(imgSize);
    
    AtmosphereData Atmosphere = GetAtmosphereData();

    vec3 cameraPos =
        vec3(g_Camera.posWORLD.x, max(g_Camera.posWORLD.y, MIN_VIEW_HEIGHT_ABOVE_GROUND), g_Camera.posWORLD.z);
    vec3 cameraPosAbovePlanet =
        cameraPos * DISTANCE_SCALE + vec3(0.0, Atmosphere.planetRadiusKm, 0.0);

    float viewHeightWORLD = length(cameraPosAbovePlanet);
    vec3  posWORLD        = vec3(0.0, viewHeightWORLD, 0.0);

    vec3 rayDir    = GetSkyViewRayDirectionFromUV(GetStretchedTextureUV(uv, vec2(imgSize)), viewHeightWORLD);
    vec3 rayOrigin = posWORLD;
    
    vec2 groundIntersection = RaySphereIntersection(rayOrigin, rayDir, PLANET_CENTER, Atmosphere.planetRadiusKm);

    float maxDistance = RAY_MARCHING_MAX_DISTANCE;
    if (groundIntersection.x > 0.0)
        maxDistance = groundIntersection.x;

    vec4 inscattered = RayMarchScattering(rayOrigin, rayDir, maxDistance);
    imageStore(g_OutSkyViewLUT, ivec2(gl_GlobalInvocationID.xy), inscattered);
}