#version 460
#extension GL_GOOGLE_include_directive : require

#include "Noise.hg"
#include "HelperFunctions.hg"

layout(set = SET_PUSH, binding = 0, r8) writeonly uniform image2D g_VerticalProfileLUT;

// Reference: https://www.guerrilla-games.com/read/nubis-evolved

layout (local_size_x = 8, local_size_y = 8) in;
void main()
{
    ivec2 imgSize   = imageSize(g_VerticalProfileLUT);
    ivec2 pixCoords = ivec2(gl_GlobalInvocationID.xy);
    if (any(greaterThanEqual(pixCoords, imgSize)))
        return;
    
    vec2  uv        = vec2(pixCoords + 0.5) / vec2(imgSize);

    float stratus       = saturate(1.0 - smoothstep(0.0, 0.1, (uv.y - 0.1) * 2.0));
    float stratoCumulus = saturate(1.0 - smoothstep(0.1, 0.6, (uv.y - 0.3) * 2.0));
    float cumulus       = saturate(1.0 - smoothstep(0.0, 0.30, uv.y - 0.7));

    float profile = mix(stratus, stratoCumulus, smoothstep(0.0, 0.25, uv.x));
          profile = mix(profile, cumulus, smoothstep(0.25, 1.0, uv.x));

    imageStore(g_VerticalProfileLUT, pixCoords, vec4(profile, profile, profile, 1.0));
}