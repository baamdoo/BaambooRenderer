#version 460
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_GOOGLE_include_directive : require

#define _CAMERA
#define _SCENEENVIRONMENT
#include "AtmosphereCommon.hg"

layout(set = 1, binding = 0) uniform sampler2D g_TransmittanceLUT;
layout(set = 1, binding = 1) uniform sampler2D g_MultiScatteringLUT;

layout(set = 1, binding = 2, rgba16f) uniform writeonly image3D g_OutAerialPerspectiveLUT;

vec4 ComputeAerialPerspective(vec3 rayOrigin, vec3 rayDir, float maxDistance, uint rayMarchSteps) 
{
    AtmosphereData Atmosphere = GetAtmosphereData();

    vec2 atmosphereIntersection = RaySphereIntersection(rayOrigin, rayDir, PLANET_CENTER, Atmosphere.atmosphereRadiusKm);
    if (atmosphereIntersection.y < 0.0)
        return vec4(0.0, 0.0, 0.0, 1.0);

    float rayStart  = max(0.0, atmosphereIntersection.x);
    float rayLength = min(maxDistance, atmosphereIntersection.y) - rayStart;
    if (rayLength <= 0.0)
        return vec4(0.0, 0.0, 0.0, 1.0);

    // light illuminance
    vec3 lightColor = vec3(Atmosphere.light.colorR, Atmosphere.light.colorG, Atmosphere.light.colorB);
    if (Atmosphere.light.temperature_K > 0.0)
        lightColor *= ColorTemperatureToRGB(Atmosphere.light.temperature_K);

    vec3 E = Atmosphere.light.illuminance_lux * lightColor;

    // phase functions
    vec3 sunDirection  = vec3(-Atmosphere.light.dirX, -Atmosphere.light.dirY, -Atmosphere.light.dirZ);

    float cosTheta      = dot(rayDir, sunDirection);
    float phaseRayleigh = RayleighPhase(cosTheta);
    float phaseMie      = MiePhase(cosTheta, Atmosphere.miePhaseG);

    float stepSize = rayLength / float(rayMarchSteps);

    vec3 L          = vec3(0.0); // In-scattered luminance
    vec3 throughput = vec3(1.0); // Transmittance
    for (uint i = 0u; i < rayMarchSteps; ++i) 
    {
        float t   = (float(i) + 0.5) * stepSize;
        vec3  pos = rayOrigin + t * rayDir;
        
        // skip if below ground
        float sampleHeight = length(pos);
        if (sampleHeight < Atmosphere.planetRadiusKm) 
            break;
        
        // skip if above atmosphere
        if (sampleHeight > Atmosphere.atmosphereRadiusKm)
            continue;

        float sampleAltitude = sampleHeight - Atmosphere.planetRadiusKm;
        
        // extinction(out-scattering) at sample point
        float rayleighDensity = GetDensityAtHeight(sampleAltitude, Atmosphere.rayleighDensityKm);
        float mieDensity      = GetDensityAtHeight(sampleAltitude, Atmosphere.mieDensityKm);
        float ozoneDensity    = GetDensityOzoneAtHeight(sampleAltitude, Atmosphere.ozoneCenterKm, Atmosphere.ozoneWidthKm);

        vec3  rayleighScattering = Atmosphere.rayleighScattering * rayleighDensity;
        float mieScattering      = Atmosphere.mieScattering * mieDensity;
        float mieAbsorption      = Atmosphere.mieAbsorption * mieDensity;
        vec3  ozoneAbsorption    = Atmosphere.ozoneAbsorption * ozoneDensity;

        vec3 scattering        = rayleighScattering + vec3(mieScattering);
        vec3 extinction        = rayleighScattering + vec3(mieScattering + mieAbsorption) + ozoneAbsorption;
        vec3 phasedScattering  = phaseRayleigh * rayleighScattering + phaseMie * mieScattering; // σs(x) * p(v,l)
        vec3 stepTransmittance = exp(-extinction * stepSize);                                   // (2) T(xa,xb) = exp(−Integral(xa~xb, σ(x)dx))

        // transmittance from sample point to sun
        float sampleTheta        = dot(normalize(pos), sunDirection);
        vec3  transmittanceToSun = SampleTransmittanceLUT(g_TransmittanceLUT, sampleHeight, sampleTheta, Atmosphere.planetRadiusKm, Atmosphere.atmosphereRadiusKm);

        // multi-scattering
        vec2 msUV = clamp(
                        vec2(sampleTheta * 0.5 + 0.5, inverseLerp(sampleHeight, Atmosphere.planetRadiusKm, Atmosphere.atmosphereRadiusKm)),
                    0.0, 1.0);
        vec3 multiScattering = texture(g_MultiScatteringLUT, msUV).rgb;

        // planet shadow
        vec2  planetIntersection = RaySphereIntersection(pos, sunDirection, PLANET_CENTER, Atmosphere.planetRadiusKm);
        float planetShadow       = planetIntersection.x < 0.0 ? 1.0 : 0.0;

        {
            // (4) S(x,li) = Vis(l_i) * T(x,x+t*li)
            // vec3 S = planetShadow * transmittanceToSun;
            // (3) Lscat(c,x,v) = σs(x) * (T(c,x) * S(x,l) * p(v,l) + Ψms) * E * dt
            // L     += (throughput * S * phasedScattering + multiScattering * scattering) * E * stepSize;
            
            // Analytical integration as proposed in slide 28 of http://www.frostbite.com/2015/08/physically-based-unified-volumetric-rendering-in-frostbite/
            vec3 S    = (planetShadow * transmittanceToSun * phasedScattering + multiScattering * scattering) * E;
            vec3 Sint = (S - S * stepTransmittance) / max(extinction, 1e-8);
            L        += throughput * Sint;
        }
        throughput *= stepTransmittance;
    }

    // xyz: (3) Lscat(c,x,v), w: (1) T(c,p) (Lo will be evaluated later stage)
    return vec4(L, dot(throughput, vec3(1.0 / 3.0)));
}

layout(local_size_x = 4, local_size_y = 4, local_size_z = 4) in;
void main()
{
    ivec3 imgSize   = imageSize(g_OutAerialPerspectiveLUT);
    ivec3 pixCoords = ivec3(gl_GlobalInvocationID.xyz);
    if (any(greaterThanEqual(pixCoords, imgSize)))
        return;

    vec3  uvw     = (vec3(pixCoords) + 0.5) / vec3(imgSize);
    
    AtmosphereData Atmosphere = GetAtmosphereData();

    vec3 cameraPos =
        vec3(g_Camera.posWORLD.x, max(g_Camera.posWORLD.y, MIN_VIEW_HEIGHT_ABOVE_GROUND), g_Camera.posWORLD.z);
    vec3 cameraPosAbovePlanet =
        cameraPos * DISTANCE_SCALE + vec3(0.0, Atmosphere.planetRadiusKm, 0.0);

    float slice = uvw.z;
    slice *= slice;
    slice *= float(imgSize.z);

    float maxDistance = AP_KM_PER_SLICE * slice;
    vec3  rayDir      = normalize(ReconstructWorldPos(uvw.xy, 0.0, g_Camera.mViewProjInv));
    vec3  rayOrigin   = cameraPosAbovePlanet;
    vec3  posInFroxel = cameraPosAbovePlanet + rayDir * maxDistance;
    
    if (length(posInFroxel) < Atmosphere.planetRadiusKm + EPSILON)
    {
        posInFroxel = normalize(posInFroxel) * (Atmosphere.planetRadiusKm + EPSILON);
        rayDir      = normalize(posInFroxel - cameraPosAbovePlanet);
        maxDistance = length(posInFroxel - cameraPosAbovePlanet);
    }

    uint rayMarchSteps = max(1, uint(gl_GlobalInvocationID.z + 1) * 2);
    vec4 result        = ComputeAerialPerspective(rayOrigin, rayDir, maxDistance, rayMarchSteps);
    
    imageStore(g_OutAerialPerspectiveLUT, ivec3(gl_GlobalInvocationID.xyz), result);
}