#version 460
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_GOOGLE_include_directive : require

#define _ATMOSPHERE
#include "AtmosphereCommon.hg"

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(set = SET_PUSH, binding = 2) uniform sampler2D g_TransmittanceLUT;
layout(set = SET_PUSH, binding = 3, r11f_g11f_b10f) uniform writeonly image2D g_MultiScatteringLUT;

// Push constants
layout(push_constant) uniform PushConstants 
{
    uint isoSampleCount;
    uint numRaySteps;
} g_Push;

vec3 ComputeMultiScattering(float viewHeight, float cosZenithAngle) 
{
    vec3 rayOrigin = vec3(0.0, viewHeight, 0.0);
    vec3 sunDir    = vec3(safeSqrt(1.0 - cosZenithAngle * cosZenithAngle), cosZenithAngle, 0.0);
    
    vec3 L2ndOrder = vec3(0.0);
    vec3 fms       = vec3(0.0);
    
    float weight       = (4.0 * PI) / float(g_Push.isoSampleCount);
    float uniformPhase = 1.0 / (4.0 * PI);

    // uniform sphere sampling(isotropic phase function)
    const float goldenRatio = (1.0 + sqrt(5.0)) / 2.0;
    for (uint i = 0u; i < g_Push.isoSampleCount; ++i) 
    {
        float theta = 2.0 * PI * float(i) / goldenRatio;
        float phi   = acos(1.0 - 2.0 * float(i) / float(g_Push.isoSampleCount));
        
        vec3 rayDir = vec3(
            sin(phi) * cos(theta),
            cos(phi),
            sin(phi) * sin(theta)
        );
        
        // compute ray length
        float rayLength              = 0.0;
        vec2  planetIntersection     = RaySphereIntersection(rayOrigin, rayDir, PLANET_CENTER, g_Atmosphere.planetRadius_km);
        vec2  atmosphereIntersection = RaySphereIntersection(rayOrigin, rayDir, PLANET_CENTER, g_Atmosphere.atmosphereRadius_km);
        if (atmosphereIntersection.y < 0.0) 
            continue;
        else if (planetIntersection.x < 0.0)
            rayLength = atmosphereIntersection.y;
        else
            rayLength = planetIntersection.x;
        
        float stepSize = rayLength / float(g_Push.numRaySteps);
        
        vec3 L          = vec3(0.0);
        vec3 Lf         = vec3(0.0);
        vec3 throughput = vec3(1.0);
        for (int step = 0; step < g_Push.numRaySteps; ++step) 
        {
            float t   = (float(step) + 0.5) * stepSize;
            vec3  pos = rayOrigin + t * rayDir;

            // skip if below ground
            float sampleHeight = length(pos);
            if (sampleHeight < g_Atmosphere.planetRadius_km) 
                break;
            
            float sampleAltitude = sampleHeight - g_Atmosphere.planetRadius_km;

            // extinction(out-scattering) at sample point
            float rayleighDensity = GetDensityAtHeight(sampleAltitude, g_Atmosphere.rayleighDensityH_km);
            float mieDensity      = GetDensityAtHeight(sampleAltitude, g_Atmosphere.mieDensityH_km);
            float ozoneDensity    = GetDensityOzoneAtHeight(sampleAltitude);
            
            vec3  rayleighScattering = g_Atmosphere.rayleighScattering * rayleighDensity;
            float mieScattering      = g_Atmosphere.mieScattering * mieDensity;
            float mieAbsorption      = g_Atmosphere.mieAbsorption * mieDensity;
            vec3  ozoneAbsorption    = g_Atmosphere.ozoneAbsorption * ozoneDensity;

            vec3 scattering        = rayleighScattering + vec3(mieScattering);
            vec3 extinction        = rayleighScattering + vec3(mieScattering + mieAbsorption) + ozoneAbsorption;
            vec3 stepTransmittance = exp(-extinction * stepSize); // (2) T(xa,xb) = exp(−Integral(xa~xb, σ(x)dx))

            // transmittance from sample point to sun
            float sampleTheta        = dot(normalize(pos), sunDir);
            vec3  transmittanceToSun = SampleTransmittanceLUT(g_TransmittanceLUT, sampleHeight, sampleTheta, g_Atmosphere.planetRadius_km, g_Atmosphere.atmosphereRadius_km);

            // planet shadow
            vec2  planetIntersection = RaySphereIntersection(pos, rayDir, PLANET_CENTER, g_Atmosphere.planetRadius_km);
            float planetShadow       = planetIntersection.x < 0.0 ? 1.0 : 0.0;

            // (4) S(x,l) = Vis(l) * T(x,x+t*l)
            vec3 S = planetShadow * transmittanceToSun;
            
            L          += scattering * throughput * S * uniformPhase * 1.0 * stepSize; // (6) L'(x,v) = @ + σs(x) * T(x,x-tv) * S(x,ws) * pu * EI * dt
            Lf         += scattering * throughput * 1.0 * stepSize;                    // (8) Lf(x,v) = σs(x) * T(x,x-tv) * 1 * dt
            throughput *= stepTransmittance;
        }

        // contribution of light scattered from the ground
        if (rayLength == planetIntersection.x)
	    {
	    	vec3 pos = rayOrigin + planetIntersection.x * rayDir;

	    	float sampleHeight = length(pos);
	    	vec3  upVec      = pos / sampleHeight;
	    	float NoL        = clamp(dot(upVec, sunDir), 0.0, 1.0);

	    	// transmittance from sample point to sun
            float sampleTheta        = dot(upVec, sunDir);
            vec3  transmittanceToSun = SampleTransmittanceLUT(g_TransmittanceLUT, sampleHeight, sampleTheta, g_Atmosphere.planetRadius_km, g_Atmosphere.atmosphereRadius_km);

            // L'(x,v) = T(x,p) * Lo(p,v), where Lo : diffuse response according to ground albedo
	    	L += throughput * transmittanceToSun * NoL * vec3(0.40981, 0.40981, 0.40981) / PI;
	    	// L += transmittanceToSun * throughput * NoL * g_Atmosphere.groundAlbedo / PI;
	    }
        
        // (5) L2ndOrder_i = L'(xs, -w) * pu * dw
        L2ndOrder += (L * uniformPhase) * weight;
        
        // (7) fms_i = Lf(xs, -w) * pu * dw
        fms += (Lf * uniformPhase) * weight;
    }
    
    // Compute infinite scattering series: 1 + f + f^2 + f^3 + ... = 1 / (1 - f)
    // This is based on the observation that after 2nd order, scattering becomes mostly isotropic
    return L2ndOrder / (vec3(1.0) - fms);
}

void main() 
{
    ivec2 texSize = imageSize(g_MultiScatteringLUT);
    vec2  uv      = vec2(gl_GlobalInvocationID.xy + 0.5) / vec2(texSize);

    if (gl_GlobalInvocationID.x >= texSize.x || gl_GlobalInvocationID.y >= texSize.y)
        return;
    
    float cosZenithAngle = 2.0 * uv.x - 1.0;
    float viewHeight     = mix(g_Atmosphere.planetRadius_km, g_Atmosphere.atmosphereRadius_km, uv.y);
    
    vec3 multiScattering = ComputeMultiScattering(viewHeight, cosZenithAngle);
    
    imageStore(g_MultiScatteringLUT, ivec2(gl_GlobalInvocationID.xy), vec4(multiScattering, 1.0));
}