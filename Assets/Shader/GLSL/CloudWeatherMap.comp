#version 460
#extension GL_GOOGLE_include_directive : require

#include "Noise.hg"
#include "HelperFunctions.hg"

layout(set = SET_PUSH, binding = 0, rg8) writeonly uniform image2D g_WeatherMap;

layout (local_size_x = 8, local_size_y = 8) in;
void main()
{
    ivec2 imgSize   = imageSize(g_WeatherMap);
    ivec2 pixCoords = ivec2(gl_GlobalInvocationID.xyz);
    if (any(greaterThanEqual(pixCoords, imgSize)))
        return;

    vec2  uv        = vec2(pixCoords + 0.5) / vec2(imgSize);

    float perlin = perlinFBM(uv, 4.0, 7, exp(-0.85), 2.0);
          perlin = mix(1.0, perlin, 0.5);
          perlin = abs(perlin * 2.0 - 1.0);
          perlin = remap(perlin, 0.0, 1.0, 0.5, 1.0);
    float worley = worleyFBM(uv, 8.0);

    float coverage  = remap(worley, perlin, 1.0, 0.0, 1.0);
    float cloudType = worleyFBM(uv, 16.0, 7, 0.5, 2.0);

    vec4 weather = vec4(saturate(coverage), saturate(cloudType), 1.0, 1.0);

    imageStore(g_WeatherMap, pixCoords, weather);
}